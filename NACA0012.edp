{
//   NACA0012
//   define borders
real pi=4*atan(1),beta;
border a(t=0,2*pi){x=5*cos(t);y=5*sin(t);label=0;};
border b(t=0,1){x=t;y=0.17735*sqrt(t)-0.075597*t
     -0.212836*(t^2)+0.17363*(t^3)-0.06254*(t^4);label=1;};
border c(t=1,2){x=2-t;y=-(0.17735*sqrt(2-t)
     -0.075597*(2-t)-0.212836*((2-t)^2)+0.17363*((2-t)^3)
	 -0.06254*((2-t)^4));label=1;};

//   define domain and mesh
mesh airfoil=buildmesh(a(64)+b(64)+c(64));
savemesh(airfoil,"NACA0012.msh");
plot(airfoil,wait=1);

//   define finite element space (P1 elements)
fespace femp1(airfoil,P1);
femp1 uh,vh,wh;
//   functions
func f=y-0.1*x;

//   define equation and solve it
//solve(psi0){
//  onbdy(1) psi0 = y-0.1*x;
//  onbdy(2) psi0 = 0;
//  pde(psi0) -laplace(psi0) = 0;
//};
//plot(psi0);


solve ps0(uh,vh)=
  int2d(airfoil)(dx(uh)*dx(vh)+dy(uh)*dy(vh)) 
 + on (0,uh=f) + on (1,2,uh=0);                               

//   plot solution
plot(uh,value=true,wait=1);

solve ps1(wh,vh)=
  int2d(airfoil)(dx(wh)*dx(vh)+dy(wh)*dy(vh)) 
 + on (0,wh=0) + on (1,2,wh=1);                               

//   plot solution
plot(wh,value=true,wait=1);

beta = uh(0.99,0.01)+uh(0.99,-0.01);
beta = -beta / (wh(0.99,0.01)+ wh(0.99,-0.01)-2);

//psi = beta*psi1 +psi0;
femp1 psi=beta*wh+uh;
femp1 cp = - dx(psi)^2  - dy(psi)^2 ;
plot(cp,value=true,wait=1);



//   save solution
 {
  ofstream file("NACA0012.out");
  file << cp[] << endl;
 }
}

